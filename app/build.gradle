apply plugin: "com.android.application"

apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"

apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion 29
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    defaultConfig {
        applicationId "app.getfeeling.feeling"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [
                hostName    : "getfeeling.app",
                defaultUrl  : "https://getfeeling.app",
                launcherName: "Feeling"
        ]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    buildTypes.each {
        Properties properties = new Properties()

        if (it.name == "release") {
            properties.load(project.rootProject.file("gradle.properties").newDataInputStream())
        } else {
            properties.load(project.rootProject.file("local.properties").newDataInputStream())
        }

        def feelingApiUrl = properties.getProperty("feelingApiUrl")
        def feelingApiClientId = properties.getProperty("feelingApiClientId")
        def feelingApiRedirectUri = properties.getProperty("feelingApiRedirectUri")
        def feelingWebsiteUrl = properties.getProperty("feelingWebsiteUrl")

        it.buildConfigField 'String', "FEELING_API_URL", feelingApiUrl
        it.buildConfigField 'String', "FEELING_API_CLIENT_ID", feelingApiClientId
        it.buildConfigField 'String', "FEELING_API_REDIRECT_URI", feelingApiRedirectUri
        it.buildConfigField 'String', "FEELING_WEBSITE_URL", feelingWebsiteUrl
    }

    // Fixes duplicate META-INF/atomicfu.kotlin_module
    packagingOptions {
        pickFirst "META-INF/kotlinx-io.kotlin_module"
        pickFirst "META-INF/atomicfu.kotlin_module"
        pickFirst "META-INF/kotlinx-coroutines-io.kotlin_module"
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    // Core dependencies
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0-alpha04'
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-beta2"

    // Core test dependencies
    testImplementation "androidx.test:core:1.2.1-alpha02"
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:2.19.0"
    testImplementation "com.google.truth:truth:1.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.2-alpha02"
    androidTestImplementation "androidx.test:runner:1.3.0-alpha02"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0-alpha02"

    // LiveData + ViewModel
    def lifecycle_version = '2.2.0-alpha05'
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    // Dagger 2
    def dagger_version = "2.24"
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    // Room
    def room_version = '2.2.0-rc01'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    // ThreeTenABP Backport for JSR-310
    implementation "com.jakewharton.threetenabp:threetenabp:1.2.1"
    testImplementation 'org.threeten:threetenbp:1.4.0'

    // Retrofit
    def retrofit2_version = '2.6.2'
    implementation "com.squareup.retrofit2:retrofit:$retrofit2_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit2_version"

    // OkHTTP
    def okhttp_version = '4.2.1'
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"

    // Moshi
    def moshi_version = "1.8.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    // Kotlin Coroutines
    def coroutine_version = "1.3.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"

    // Chrome Custom Tabs
    def chrome_custom_tabs_version = "a0f7418972"
    implementation "com.github.GoogleChrome:custom-tabs-client:$chrome_custom_tabs_version"

    // Preferences
    implementation 'androidx.preference:preference:1.1.0'

    // Secure Storage
    def secure_storage_version = "1.2.2"
    implementation "de.adorsys.android:securestoragelibrary:$secure_storage_version"

    // Material Design
    implementation 'com.google.android.material:material:1.1.0-alpha10'

    // JWT
    def jwt_decode_version = "1.3.0"
    implementation "com.auth0.android:jwtdecode:$jwt_decode_version"

    // Room Debug Database
    def room_debug_database_version = "1.0.6"
    debugImplementation "com.amitshekhar.android:debug-db:$room_debug_database_version"

    // Paging
    implementation 'androidx.paging:paging-runtime-ktx:2.1.0'
}
